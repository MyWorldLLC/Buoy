plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    mavenCentral()
}

def getGitVersion(){
    "git describe --tags --always --first-parent".execute().text.trim()
}

version = getGitVersion()
group = 'com.myworldvw'

dependencies {

    testImplementation "org.spockframework:spock-core:2.3-groovy-3.0"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'

}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(20)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add('--enable-preview')
}

compileTestGroovy {
    options.compilerArgs.add('--enable-preview')
}

test {
    useJUnitPlatform()
    // TODO - support non-Linux lib names
    systemProperty "buoy.test.lib.path", new File(parent.projectDir.absoluteFile, "native/build/lib/main/debug/").toPath().toString()
    jvmArgs(['--enable-preview'])
}

publishing {
    publications {
        library(MavenPublication) {

            artifactId = 'buoy'

            from components.java
        }
    }
    repositories {
        mavenLocal()
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/MyWorldLLC/Packages")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}